apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def envOrProp = { key, defaultValue = null ->
    return System.getenv(key) ?: keystoreProperties.getProperty(key) ?: defaultValue
}

def releaseStoreFile = envOrProp("FASKET_KEYSTORE_PATH", keystoreProperties.getProperty("storeFile"))
def releaseStorePassword = envOrProp("FASKET_KEYSTORE_PASS", keystoreProperties.getProperty("storePassword"))
def releaseKeyAlias = envOrProp("FASKET_KEY_ALIAS", keystoreProperties.getProperty("keyAlias"))
def releaseKeyPassword = envOrProp("FASKET_KEY_PASS", keystoreProperties.getProperty("keyPassword"))
def hasReleaseSigning = releaseStoreFile && releaseStorePassword && releaseKeyAlias && releaseKeyPassword

//def envVersionCode = System.getenv("VERSION_CODE")
//def envVersionName = System.getenv("VERSION_NAME")
//def parsedVersionCode = 1
//try {
//    parsedVersionCode = envVersionCode ? Integer.parseInt(envVersionCode) : 2
//} catch(Exception ignored) {}

android {
    namespace "fasket.user"
    compileSdk rootProject.ext.compileSdkVersion
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    defaultConfig {
        // todo: Set Android app version here
        versionCode 5
        versionName "1.0.0"
        applicationId "fasket.user"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
            // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
            // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        if (hasReleaseSigning) {
            release {
                storeFile file(releaseStoreFile)
                storePassword releaseStorePassword
                keyAlias releaseKeyAlias
                keyPassword releaseKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            } else {
                println("WARNING: Release signing config missing. Build will be unsigned; set keystore vars before uploading to Play.")
            }
        }
        debug {
            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            }
        }
    }

}

repositories {
    flatDir {
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch (ignored) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
